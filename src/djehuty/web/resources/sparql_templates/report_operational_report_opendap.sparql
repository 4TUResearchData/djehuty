# Opendap data urls in a given date interval separated by member institution
# For this query, ***DISABLE*** "Strict checking of void variables" !
# When checking is enabled (default):
#   Virtuoso 37000 Error SP031: SPARQL compiler: Variable '_::trans_subj_20_29' is used in subexpressions of the query but not assigned
#   see also https://sourceforge.net/p/virtuoso/mailman/message/33073869/

PREFIX djht:  <https://ontologies.data.4tu.nl/djehuty/0.0.1/>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graph: <https://data.4tu.nl>

SELECT DISTINCT ?institution ?data_url
WHERE
{
  { SELECT DISTINCT MIN(?posted_date) AS ?date
                    ?data_url ?gid
    WHERE {
      GRAPH graph: {
        ?container  a                                           djht:DatasetContainer ;
                    djht:latest_published_version/djht:group_id ?gid ;
                    djht:published_versions/rdf:rest*/rdf:first ?dataset .
        ?dataset    djht:posted_date                            ?posted_date .
        { ?dataset  djht:files/rdf:rest*/rdf:first              ?file .
          ?file     djht:is_link_only                           true ;
                    djht:download_url                           ?data_url . } UNION
        { ?dataset  djht:data_link                              ?data_url . }
      }
      FILTER (STRSTARTS(?data_url, "https://opendap.4tu.nl/thredds/"))
    }
  }
  BIND (   1*(?gid=28586 OR ?gid=28628) #Delft
         + 2*(?gid=28589 OR ?gid=28631) #Eindhoven
         + 3*(?gid=28592 OR ?gid=28634) #Twente
         + 4*(?gid=28595)               #Wageningen
         AS ?institution_num )
  VALUES (?institution_num ?institution) {
    (0 "other") (1 "Delft") (2 "Eindhoven") (3 "Twente") (4 "Wageningen")
  }
  FILTER ( STR(?date) > "{{BEGIN}}" AND STR(?date) < "{{END}}")
}
ORDER BY ?institution