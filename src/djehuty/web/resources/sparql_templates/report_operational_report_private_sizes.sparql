# Stats of total size of unpublished uploads in a given date interval
# separated by member institution

PREFIX djht:  <https://ontologies.data.4tu.nl/djehuty/0.0.1/>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX graph: <https://data.4tu.nl>

SELECT DISTINCT ?institution
                SUM(?size) AS ?total_size

WHERE { GRAPH graph: {
    ?container a                              djht:DatasetContainer ;
               djht:draft                     ?draft .
    ?draft     djht:created_date              ?date ;
               djht:group_id                  ?gid ;
               djht:files/rdf:rest*/rdf:first ?file .
    ?file      djht:is_link_only              false ;
               djht:size                      ?size .
    FILTER NOT EXISTS { ?container djht:published_versions/rdf:rest*/rdf:first/djht:files/rdf:rest*/rdf:first ?file . }
    BIND (   1*(?gid=28586 OR ?gid=28628) #Delft
           + 2*(?gid=28589 OR ?gid=28631) #Eindhoven
           + 3*(?gid=28592 OR ?gid=28634) #Twente
           + 4*(?gid=28595)               #Wageningen
           AS ?institution_num )
    VALUES (?institution_num ?institution) {
      (0 "other") (1 "Delft") (2 "Eindhoven") (3 "Twente") (4 "Wageningen")
    }
    FILTER ( STR(?date) > "{{BEGIN}}" AND STR(?date) < "{{END}}")
  }
}
GROUP BY ?institution
ORDER BY ?institution
